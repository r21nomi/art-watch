// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.0'
        classpath 'com.google.gms:google-services:3.0.0'
        classpath 'com.github.triplet.gradle:play-publisher:1.1.5'
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

ext {
    APPLICATION_ID = "com.nomi.artwatch"
    COMPILE_SDK_VERSION = 23
    BUILDTOOLS_VERSION = "23.0.1"
    MIN_SDK_VERSION = 21
    TARGET_SDK_VERSION = 23
    VERSION_CODE = getVersion()
    VERSION_NAME = getVersionName()
    SUPPORT_LIBRARY_VERSION = "24.2.1"
    SUPPORT_WEARABLE_VERSION = "1.4.0"
    PLAY_SERVICES_VERSION = "9.4.0"
    FIREBASE_VERSION = "9.4.0"
    KOTLIN_VERSION = '1.0.3'

    signingConfigs = {
        release {
            storeFile file(String.valueOf(System.getenv("ANDROID_KEY_STORE_FILE")))
            storePassword System.getenv("ANDROID_KEY_STORE_PASSWORD")
            keyAlias System.getenv("ANDROID_KEY_ALIAS")
            keyPassword System.getenv("ANDROID_KEY_PASSWORD")
        }
    }
}

/**
 * Get version from versionName
 * ex. If versionName is v1.1.2, the version will be 4.
 *
 * @return
 */
def getVersion() {
    def numbers = getVersionName().split(/\./)
    int number = 0

    for (i in numbers) {
        number += i.toInteger()
    }
    return number
}

/**
 * Get versionName as String exclude "v".
 *
 * @return
 */
def getVersionName() {
    def tag = System.getenv("TAG_NAME")
    if (tag == null || tag == "" || tag == " ") {
        return "1.0.0"
    }
    return tag.minus("v")
}

task printVersions {
    doLast {
        println "versionName : " + getVersionName()
        println "version : " + getVersion()
    }
}