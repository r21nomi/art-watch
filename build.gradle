// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.0'
        classpath 'com.google.gms:google-services:3.0.0'
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

ext {
    APPLICATION_ID = "com.nomi.artwatch"
    COMPILE_SDK_VERSION = 25
    BUILDTOOLS_VERSION = "25.0.2"
    MIN_SDK_VERSION = 21
    TARGET_SDK_VERSION = 25
    VERSION_CODE = getVersion()
    VERSION_NAME = getVersionName()
    SUPPORT_LIBRARY_VERSION = "25.3.1"
    SUPPORT_WEARABLE_VERSION = "2.0.1"
    PLAY_SERVICES_VERSION = "10.2.1"
    KOTLIN_VERSION = '1.1.1'

    signingConfigs = {
        release {
            storeFile file(String.valueOf(System.getenv("ANDROID_KEY_STORE_FILE")))
            storePassword System.getenv("ANDROID_KEY_STORE_PASSWORD")
            keyAlias System.getenv("ANDROID_KEY_ALIAS")
            keyPassword System.getenv("ANDROID_KEY_PASSWORD")
        }
    }

    dexOptions = {
        jumboMode true
        maxProcessCount 4
    }
}

/**
 * Get version from versionName
 * ex. If versionName is v1.1.2, the version will be 4.
 *
 * @return
 */
def getVersion() {
    def numbers = getVersionName().split(/\./)
    int number = 0

    for (i in numbers) {
        number += i.toInteger()
    }
    return number
}

/**
 * Get versionName as String exclude "v".
 *
 * @return
 */
def getVersionName() {
    def tag = System.getenv("TAG_NAME")
    if (tag == null || tag == "" || tag == " ") {
        return "1.0.0"
    }
    return tag.minus("v")
}

task printVersions {
    doLast {
        println "versionName : " + getVersionName()
        println "version : " + getVersion()
    }
}